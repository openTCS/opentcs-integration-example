apply from: "${rootDir}/gradle/dependency-versions.gradle"
apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-artifacts.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply plugin: 'org.kordamp.gradle.stats'

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.kernel.RunKernel'
}
mainClassName = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

configurations {
  kernelJar
}

dependencies {
  implementation project(':openTCS-Example-KernelExtension-Statistics')

  implementation group: 'org.opentcs', name: 'opentcs-kernel', version: openTcsBaselineVersion
  implementation group: 'org.opentcs.commadapter.vehicle.vda5050', name: 'opentcs-commadapter-vda5050', version: vda5050CommAdapterVersion
  kernelJar group: 'org.opentcs', name: 'opentcs-kernel', version: openTcsBaselineVersion, ext: 'jar'
}

distributions {
  main {
    contents {
      from (zipTree(configurations.kernelJar.singleFile)) {
        include 'org/opentcs/kernel/distribution/**'

        eachFile { file ->
          // Remove the top-level folders. We only want the content of '/distribution'
          file.path = file.path.replaceFirst("(.*)/distribution", "")
        }

        includeEmptyDirs false
      }
      from "${sourceSets.main.resources.srcDirs[0]}/com/example/kernel/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'java.security.policy':'file:./config/java.policy',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.cfg4j.reload.interval':'10000'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow')
}
