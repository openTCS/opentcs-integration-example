apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-artifacts.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply from: "${rootDir}/gradle/publishing-java.gradle"

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.kernel.RunKernel'
}
application.mainClass = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

configurations {
  kernelJar
}

dependencies {
  implementation project(':openTCS-Example-KernelExtension-Statistics')

  implementation libs.opentcs.kernel
  implementation libs.opentcs.commadapter.vda5050
  kernelJar group: 'org.opentcs', name: 'opentcs-kernel', version: libs.versions.opentcs.baseline.get(), ext: 'jar'
}

distributions {
  main {
    contents {
      from (zipTree(configurations.kernelJar.singleFile)) {
        include 'org/opentcs/kernel/distribution/**'

        eachFile { file ->
          // Remove the top-level folders. We only want the content of '/distribution'
          file.path = file.path.replaceFirst("(.*)/distribution", "")
        }

        includeEmptyDirs false
      }
      from "${sourceSets.main.resources.srcDirs[0]}/com/example/kernel/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.cfg4j.reload.interval':'10000',\
                    'opentcs.configuration.reload.interval':'10000',\
                    'opentcs.configuration.provider':'gestalt'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow')
}
